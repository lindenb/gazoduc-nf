/*

Copyright (c) 2025 Pierre Lindenbaum

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
The MIT License (MIT)
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/


process VCF_TO_BED {
label "process_single"

tag "${meta.id?:""}"
conda "${moduleDir}/../../../conda/bioinfo.01.yml"
input:
	tuple val(meta),path(vcf),path(optional_idx)
output:
	tuple val(meta),path("*.bed"),path(vcf),path(optional_idx),optional:true,emit:output
	tuple val(meta),path("*.bed"),optional:true,emit:bed
	path("versions.yml"),emit:versions
script:	
	def prefix= task.ext.prefix?:"${meta.id}.vcf2bed"
	def awk_expr = task.ext.awk_expr?:""
	if(task.ext.split==null) throw new IllegalArgumentException("ext.split not defined for ${task.process}");
	// if TRUE : split the final file of N bed records to N files with one bed record
	def split = (task.ext.split?:false) as boolean
"""
hostname 1>&2
set -o pipefail
mkdir -p TMP


# vcf generated by htsjdk don't have an idx that contains metadata form bcftools index -s, let's try
if ${(optional_idx?true:false) && task.attempt==1 }
then

bcftools index -s "${vcf}" |\\
	awk -F '\t' '${awk_expr}{printf("%s\t0\t%s\\n",\$1,\$2);}' |\\
	LC_ALL=C sort -T TMP -t '\t' -k1,1 -k2,2n > TMP/jeter.bed

else

bcftools query -f '%CHROM\t%POS0\t%END\\n' "${vcf}" |\\
	awk -F '\t' '
		{
		C=\$1; B=int(\$2); E=int(\$3);
		if(!(C in m) || B<m[C]) {m[C]=B;} 
		if(!(C in M) || E>M[C]) {M[C]=E;}
		}
		END {for(C in m) {printf("%s\t%d\t%d\\n",C,m[C],M[C]);}}' |\\
	LC_ALL=C sort -T TMP -t '\t' -k1,1 -k2,2n > TMP/jeter.bed

fi


if test -s TMP/jeter.bed
then
	if ${split}
	then
		split --lines=1 --additional-suffix=.bed -a 9 TMP/jeter.bed "${prefix}."
	else
		mv TMP/jeter.bed "${prefix}.bed"
	fi
fi


cat << EOF > versions.yml
"${task.process}":
    bcftools: \$(bcftools version | awk '(NR==1)  {print \$NF}')
EOF
"""
stub:
if(task.ext.split==null) throw new IllegalArgumentException("ext.split not defined for ${task.process}");
def prefix = "${meta.id}.vcf2bed"
def split=false
"""
touch versions.yml split.${meta.id}.bed

if ${vcf.endsWith("*.vcf.gz")}
then

gunzip -c "${vcf}" |\\
	awk -F '\t' '
		/^#/ {next;}
		{
		C=\$1; P=int(\$2);
		if(!(C in m) || (P-1)<m[C]) {m[C]=P-1;} 
		if(!(C in M) || P>M[C]) {M[C]=P;}
		}
		END {for(C in m) {printf("%s\t%d\t%d\\n",C,m[C],M[C]);}}' |\\
		LC_ALL=C sort -T TMP -t '\t' -k1,1 -k2,2n > TMP/jeter.bed
fi

if test -s TMP/jeter.bed
then
	if ${split}
	then
		split --lines=1 --additional-suffix=.bed -a 9 TMP/jeter.bed "${prefix}."
	else
		mv TMP/jeter.bed "${prefix}.bed"
	fi
fi

"""
}
